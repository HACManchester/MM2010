+++++ +++++
[
    >+++++ ++               capital
    >+++++ +++++            lowercase
    >++++                   comma
    >+++                    space
    >+++++ +                question mark
    >+                      newline
    <<<<< < -
]
> ++ .                      print H
> +++++ .                   print i
> ++++ .                    print comma
> ++ .                      print space
<< ++++ .                   print m
+++++ +++++ ++ .            print y
>> .
<< ----- ----- - .          print n
----- ----- --- .           print a
+++++ +++++ ++ .            print m
----- --- .                 print e
>> .
<< ++++ .                   print i
+++++ +++++ .               print s
>> .
<<< +++++ +++++ +++++ +++ . print Z
> ----- ----- ---- .        print e
+++++ + .                   print k
----- - .                   print e
> ++ . --                   print dot
> .
<<< --- .                   print W
> +++ .                     print h
----- -- .                  print a
+++++ +++++ +++++ ++++ .    print t
>> .
<< ----- ----- - .          print i
+++++ +++++ .               print s
>> .
<< +++++ + .                print y
----- ----- .               print o
+++++ + .                   print u
--- .                       print r
>> .
<< ---- .                   print n
----- ----- --- .           print a
+++++ +++++ ++ .            print m
----- --- .                 print e
>>> +++ .                   print ?
> .                         print \n
>,[>,]                      get name input
<<[<]>                      go back to beginning
----- ----- ----- .         print H
> .                         print e
+++++ ++ . .                print l l
+++ .                       print o
>> . >>> [.>]               space then print name
<<[<]>                      back to beginning
>> . > . <<
+ .                         print p
---- .                      print l
------- .                   print e
---- .                      print a
+++++ +++++ +++++ +++ .     print s
----- ----- ---- .          print e
- .                         print d
>> . <<
+++++ +++++ +++++ + .       print t
----- .                     print o
>> . <<
-- .                        print m
----- --- ..                print ee
+++++ +++++ +++++ .         print t
>> . <<
+++++ .                     print y
----- ----- .               print o
+++++ + .                   print u
> ++ .
>>> .                       print \n
